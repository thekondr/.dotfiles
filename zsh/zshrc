#
# Executes commands at the start of an interactive session.
#

[[ -z "$TMUX" ]] \
    && [[ "$OSTYPE" != cygwin ]] \
    && [[ "$TERM" != dumb ]] \
    && export TERM=xterm-256color

fpath=($HOME/.dotfiles/zsh/functions $fpath)

zstyle ':prezto:*:*' color 'yes'
zstyle ':prezto:load' zfunction 'zargs' 'zmv'
zstyle ':prezto:load' pmodule \
       'environment' \
       'terminal' \
       'editor' \
       'git' \
       'gnu-utility' \
       'history' \
       'directory' \
       'spectrum' \
       'utility' \
       'completion' \
       'history-substring-search' \
       'prompt'
zstyle ':prezto:module:editor' key-bindings 'emacs'
zstyle ':prezto:module:gnu-utility' prefix 'g'
zstyle ':prezto:module:syntax-highlighting' styles \
       'unknown-token' 'fg=red' \
       'builtin' 'fg=yellow' \
       'alias' 'fg=blue' \
       'command' 'fg=green' \
       'path' 'fg=cyan' \
       'path_prefix' 'fg=cyan' \
       'function' 'fg=magenta'
zstyle ':prezto:module:prompt' theme 'tk'

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

setopt CLOBBER

bindkey -s '\el' 'ls\n'
bindkey -s '\ej' 'gws\n'
bindkey -s '\ek' 'gwd\n'

if [[ -n "$INSIDE_EMACS" ]] && [[ "$TERM" != dumb ]]; then
    function multi-term-chpwd() {
        print -P "\033AnSiTc %d"
    }
    chpwd_functions=(${chpwd_functions[@]} 'multi-term-chpwd')
    print -P "\033AnSiTu %n"
    print -P "\033AnSiTc %d"
fi

function apply-color-theme() {
    source ~/.dotfiles/shell/base16-tomorrow.dark.sh
}

[[ -z "$INSIDE_EMACS" ]] \
    && [[ "$TERM" != dumb ]] \
    && apply-color-theme

unalias rm
unalias mv
unalias cp
unalias ln

alias rf='rm -rf'

function take() {
    mkdir -p $1
    cd $1
}

function rfme() {
    local name="$(basename $(pwd))"
    cd ..
    rf ${name}
}

function rfr() {
    local name="$(basename $(pwd))"
    cd ..
    rf ${name}
    take ${name}
}

function print-public-key() {
    cat ~/.ssh/id_rsa.pub
}

function authorize-key() {
    print-public-key | ssh $@ 'cat - >> ~/.ssh/authorized_keys'
}

alias vissh='vim ~/.ssh/config'
alias vz='vim-source ~/.zshrc'
alias vv='vim ~/.vimrc'
alias s='eval sudo $(fc -nl -1)'
alias ta='tmux attach'

alias find='noglob find'
alias gfap='git fetch --all -p'

alias pd='popd'

if [[ "$OSTYPE" != darwin* ]]; then
    eval $(dircolors ~/.dotfiles/shell/dircolors.base16.dark)
fi
