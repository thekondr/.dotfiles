[ -z "$TMUX" ] && export TERM=xterm-256color

if [ -n "$INSIDE_EMACS" ]; then
    multi-term-chpwd() {
        print -P "\033AnSiTc %d"
    }
    chpwd_functions=(${chpwd_functions[@]} 'multi-term-chpwd')
    print -P "\033AnSiTu %n"
    print -P "\033AnSiTc %d"
fi

export ZSH=$HOME/.dotfiles/zsh
export EDITOR=vim

fpath=($ZSH/functions $ZSH/completions $fpath)

if [[ -z "$ZSH_PRIVATE" ]]; then
    ZSH_PRIVATE="$ZSH/private"
fi

if [[ -z "$ZSH_CACHE_DIR" ]]; then
    ZSH_CACHE_DIR="$ZSH/cache/"
fi

for config_file ($ZSH/lib/*.zsh); do
    source $config_file
done

fpath=($ZSH/git $fpath)

if [[ "$OSTYPE" = darwin* ]]; then
    # OS X's $HOST changes with dhcp, etc. Use ComputerName if possible.
    SHORT_HOST=$(scutil --get ComputerName 2>/dev/null) || SHORT_HOST=${HOST/.*/}
else
    SHORT_HOST=${HOST/.*/}
fi

if [ -z "$ZSH_COMPDUMP" ]; then
    ZSH_COMPDUMP="${ZDOTDIR:-${HOME}}/.zcompdump-${SHORT_HOST}-${ZSH_VERSION}"
fi

autoload -U compinit
compinit -i -d "${ZSH_COMPDUMP}"

source $ZSH/git/git.zsh
source $ZSH/theme.zsh

for config_file ($ZSH_PRIVATE/*.zsh(N)); do
    source $config_file
done
unset config_file

apply-color-theme() {
    local theme=${1:-base16-tomorrow.dark}
    source ~/.dotfiles/shell/$theme.sh
}

if [ -z "$INSIDE_EMACS" ]; then
    apply-color-theme
fi

vim-source() {
    vim $1 && . $1
}

vim-private-zsh() {
    vim-source "$ZSH_PRIVATE/$1.zsh"
}

rm-private-zsh() {
    rm "$ZSH_PRIVATE/$1.zsh"
}

alias vis=vim-source
alias vpz=vim-private-zsh
alias rmpz=rm-private-zsh

alias rf='rm -rf'

rfme() {
    local name="$(basename $(pwd))"
    cd ..
    rf ${name}
}

rfr() {
    local name="$(basename $(pwd))"
    cd ..
    rf ${name}
    take ${name}
}

alias vz='vim-source ~/.zshrc'
alias vv='vim ~/.vimrc'
alias s='eval sudo $(fc -nl -1)'

alias vs='vagrant ssh'
alias vup='vagrant up && vagrant ssh'
alias vd='vagrant destroy'
alias vh='vagrant halt'

alias pd='popd'

bck() {
    for file in $@
    do
        mv $file $file.old
    done
}

uck() {
    for file in $@
    do
        mv $file.old $file
    done
}

if [[ ! "$OSTYPE" = darwin* ]]; then
    eval `dircolors ~/.dotfiles/shell/dircolors.ansi-dark`
fi
